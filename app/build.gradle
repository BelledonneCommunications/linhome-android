apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.google-services'
apply plugin: "androidx.navigation.safeargs"
apply plugin: 'com.google.firebase.crashlytics'

repositories {
    maven {
        url file(LinphoneSdkBuildDir + '/maven_repository/')
    }
    maven {
        url "https://linphone.org/maven_repository"
    }
    maven {
        url file('../linphone-sdk/')
    }
    maven {
        url 'https://jitpack.io'
    }
}

task zipTheme(type: Zip){
    println "Zipping theme"
    from '../lindoor-shared-themes/'
    include 'action_types.xml'
    include 'method_types.xml'
    include 'device_types.xml'
    include 'texts.xml'
    include 'theme.xml'
    include 'images/**/*'
    include 'fonts/**/*'
    archiveFileName = "lindoor.zip"
    destinationDirectory = file('src/main/assets/')
}

afterEvaluate {
    for(def task in it.tasks)
        if(task != tasks.zipTheme)
            task.dependsOn tasks.zipTheme
}

android {
    compileSdkVersion 29

    defaultConfig {
        applicationId "org.lindoor"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode 8
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    applicationVariants.all { variant ->
        variant.getMergedFlavor().manifestPlaceholders = [lindoor_file_provider: applicationId + ".fileprovider"]
    }

    signingConfigs {
        release {
            def keystorePropertiesFile = rootProject.file("keystore.properties")
            def keystoreProperties = new Properties()
            keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }

    buildFeatures{
        dataBinding = true
    }


    buildTypes {
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            firebaseCrashlytics {
                nativeSymbolUploadEnabled true
                strippedNativeLibsDir "build/intermediates/stripped_native_libs/release/out/lib"
                unstrippedNativeLibsDir "build/intermediates/merged_native_libs/release/out/lib"
            }
        }
        debug {
            firebaseCrashlytics {
                nativeSymbolUploadEnabled true
                strippedNativeLibsDir "build/intermediates/stripped_native_libs/debug/out/lib"
                unstrippedNativeLibsDir "build/intermediates/merged_native_libs/debug/out/lib"
            }
        }

    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.3.1'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
    implementation 'androidx.navigation:navigation-fragment:2.3.0'
    implementation 'androidx.navigation:navigation-ui:2.3.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.0'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    implementation "org.linphone:linphone-sdk-android:$linphone_sdk_version"
    implementation "org.permissionsdispatcher:permissionsdispatcher:4.7.0"
    kapt "org.permissionsdispatcher:permissionsdispatcher-processor:4.7.0"
    implementation "com.google.android.material:material:1.1.0"
    implementation 'commons-codec:commons-codec:1.10'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.4"
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    kapt 'com.github.bumptech.glide:compiler:4.11.0'
    implementation 'com.caverock:androidsvg-aar:1.4'
    implementation 'com.google.firebase:firebase-messaging:20.2.3'
    implementation 'com.google.firebase:firebase-analytics:17.4.4'
    implementation 'com.google.firebase:firebase-crashlytics:17.1.1'
    implementation 'com.github.ybq:Android-SpinKit:1.4.0'
    implementation "androidx.media:media:1.1.0"
    implementation 'com.google.firebase:firebase-crashlytics-ndk:17.1.1'


}


