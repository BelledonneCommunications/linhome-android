apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-parcelize'
apply plugin: 'com.google.gms.google-services'
apply plugin: "androidx.navigation.safeargs"
apply plugin: 'com.google.firebase.crashlytics'

task zipTheme(type: Zip){
    println "Zipping theme"
    from '../linhome-shared-themes/'
    include 'action_types.xml'
    include 'method_types.xml'
    include 'device_types.xml'
    include 'texts.xml'
    include 'theme.xml'
    include 'images/**/*'
    include 'fonts/**/*'
    archiveFileName = "linhome.zip"
    destinationDirectory = file('src/main/assets/')
}

afterEvaluate {
    for(def task in it.tasks)
        if(task != tasks.zipTheme)
            task.dependsOn tasks.zipTheme
}

task getGitVersion() {
    def gitVersion = "5.0.0"
    def gitVersionStream = new ByteArrayOutputStream()
    def gitCommitsCount = new ByteArrayOutputStream()
    def gitCommitHash = new ByteArrayOutputStream()

    try {
        exec {
            executable "git" args "describe", "--abbrev=0"
            standardOutput = gitVersionStream
        }
        exec {
            executable "git" args "rev-list", gitVersionStream.toString().trim() + "..HEAD", "--count"
            standardOutput = gitCommitsCount
        }
        exec {
            executable "git" args "rev-parse", "--short", "HEAD"
            standardOutput = gitCommitHash
        }

        if (gitCommitsCount.toString().toInteger() == 0) {
            gitVersion = gitVersionStream.toString().trim()
        } else {
            gitVersion = gitVersionStream.toString().trim() + "." + gitCommitsCount.toString().trim() + "+" + gitCommitHash.toString().trim()
        }
        println("Git version: " + gitVersion)
    } catch (ignored) {
        println("Git not found")
    }
    project.version = gitVersion
}
project.tasks['preBuild'].dependsOn 'getGitVersion'

def crashlyticsForNdkEnabled = new File(projectDir.absolutePath +'/google-services.json').exists() && new File(LinphoneSdkBuildDir + '/libs/').exists() && new File(LinphoneSdkBuildDir + '/libs-debug/').exists()

android {

    compileSdk 34

    defaultConfig {
        applicationId "org.linhome"
        minSdkVersion 23
        targetSdkVersion 34
        versionCode 31
        versionName "1.3"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    applicationVariants.all { variant ->
        variant.getMergedFlavor().manifestPlaceholders = [linhome_file_provider: applicationId + ".fileprovider"]
        variant.outputs.all {
            outputFileName = "linhome-android-${variant.buildType.name}-${project.version}.apk"
        }
    }

    signingConfigs {
        release {
            def keystorePropertiesFile = rootProject.file("keystore.properties")
            def keystoreProperties = new Properties()
            keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }

    buildFeatures{
        dataBinding = true
        viewBinding = true
    }


    buildTypes {
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if (crashlyticsForNdkEnabled) {
                firebaseCrashlytics {
                    nativeSymbolUploadEnabled true
                    unstrippedNativeLibsDir file(LinphoneSdkBuildDir + '/libs-debug/').toString()
                }
            }
        }
        debug {
            if (crashlyticsForNdkEnabled) {
                firebaseCrashlytics {
                    nativeSymbolUploadEnabled true
                    unstrippedNativeLibsDir file(LinphoneSdkBuildDir + '/libs-debug/').toString()
                }
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    namespace 'org.linhome'

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
    implementation 'androidx.navigation:navigation-fragment:2.7.6'
    implementation 'androidx.navigation:navigation-ui:2.7.6'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.7.6'
    implementation 'androidx.navigation:navigation-ui-ktx:2.7.6'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    implementation "org.linphone:linphone-sdk-android:5.3+"
    implementation 'com.github.permissions-dispatcher:permissionsdispatcher:4.9.2'
    kapt 'com.github.permissions-dispatcher:permissionsdispatcher-processor:4.9.2'
    implementation "com.google.android.material:material:1.11.0"
    implementation 'commons-codec:commons-codec:1.15'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.1"
    implementation 'com.github.bumptech.glide:glide:4.15.0'
    kapt 'com.github.bumptech.glide:compiler:4.12.0'
    implementation 'com.caverock:androidsvg-aar:1.4'
    implementation 'com.google.firebase:firebase-messaging:23.4.0'
    implementation 'com.google.firebase:firebase-analytics:21.5.0'
    implementation 'com.google.firebase:firebase-crashlytics:18.6.0'
    implementation 'com.github.ybq:Android-SpinKit:1.4.0'
    implementation "androidx.media:media:1.7.0"
    if (crashlyticsForNdkEnabled) {
        implementation 'com.google.firebase:firebase-crashlytics-ndk:18.3.7'
    } else {
        compileOnly 'com.google.firebase:firebase-crashlytics-ndk:18.3.7'
    }
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.2.0-alpha01'

}

if (crashlyticsForNdkEnabled) {
    afterEvaluate {
        assembleRelease.finalizedBy(uploadCrashlyticsSymbolFileRelease)
        assembleDebug.finalizedBy(uploadCrashlyticsSymbolFileDebug)
    }
}



