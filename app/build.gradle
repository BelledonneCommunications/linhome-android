apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-parcelize'
apply plugin: 'com.google.gms.google-services'
apply plugin: "androidx.navigation.safeargs"
apply plugin: 'com.google.firebase.crashlytics'


repositories {
    maven {
        url file(LinphoneSdkBuildDir + '/maven_repository/')
    }
    maven {
        url "https://linphone.org/maven_repository"
    }
    maven {
        url file('../linphone-sdk/')
    }
    maven {
        url 'https://jitpack.io'
    }
}

task zipTheme(type: Zip){
    println "Zipping theme"
    from '../linhome-shared-themes/'
    include 'action_types.xml'
    include 'method_types.xml'
    include 'device_types.xml'
    include 'texts.xml'
    include 'theme.xml'
    include 'images/**/*'
    include 'fonts/**/*'
    archiveFileName = "linhome.zip"
    destinationDirectory = file('src/main/assets/')
}

afterEvaluate {
    for(def task in it.tasks)
        if(task != tasks.zipTheme)
            task.dependsOn tasks.zipTheme
}

task getGitVersion() {
    def gitVersion = "5.0.0"
    def gitVersionStream = new ByteArrayOutputStream()
    def gitCommitsCount = new ByteArrayOutputStream()
    def gitCommitHash = new ByteArrayOutputStream()

    try {
        exec {
            executable "git" args "describe", "--abbrev=0"
            standardOutput = gitVersionStream
        }
        exec {
            executable "git" args "rev-list", gitVersionStream.toString().trim() + "..HEAD", "--count"
            standardOutput = gitCommitsCount
        }
        exec {
            executable "git" args "rev-parse", "--short", "HEAD"
            standardOutput = gitCommitHash
        }

        if (gitCommitsCount.toString().toInteger() == 0) {
            gitVersion = gitVersionStream.toString().trim()
        } else {
            gitVersion = gitVersionStream.toString().trim() + "." + gitCommitsCount.toString().trim() + "+" + gitCommitHash.toString().trim()
        }
        println("Git version: " + gitVersion)
    } catch (ignored) {
        println("Git not found")
    }
    project.version = gitVersion
}
project.tasks['preBuild'].dependsOn 'getGitVersion'


android {
    compileSdkVersion 31

    defaultConfig {
        applicationId "org.linhome"
        minSdkVersion 23
        targetSdkVersion 31
        versionCode 26
        versionName "${project.version}"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    applicationVariants.all { variant ->
        variant.getMergedFlavor().manifestPlaceholders = [linhome_file_provider: applicationId + ".fileprovider"]
        variant.outputs.all {
            outputFileName = "linhome-android-${variant.buildType.name}-${project.version}.apk"
        }
    }

    signingConfigs {
        release {
            def keystorePropertiesFile = rootProject.file("keystore.properties")
            def keystoreProperties = new Properties()
            keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }

    buildFeatures{
        dataBinding = true
        viewBinding = true
    }


    buildTypes {
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            firebaseCrashlytics {
                nativeSymbolUploadEnabled true
                unstrippedNativeLibsDir "build/intermediates/merged_native_libs/release/out/lib"
            }
        }
        debug {
            firebaseCrashlytics {
                nativeSymbolUploadEnabled true
                unstrippedNativeLibsDir "build/intermediates/merged_native_libs/debug/out/lib"
            }
        }

        packagingOptions {
            exclude 'META-INF/DEPENDENCIES'
        }

    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }

}

dependencies {
    implementation platform('com.google.firebase:firebase-bom:25.12.0')
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
    implementation 'androidx.navigation:navigation-fragment:2.4.1'
    implementation 'androidx.navigation:navigation-ui:2.4.1'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.4.1'
    implementation 'androidx.navigation:navigation-ui-ktx:2.4.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    implementation "org.linphone:linphone-sdk-android:5.2+"
    implementation "org.permissionsdispatcher:permissionsdispatcher:4.8.0"
    kapt "org.permissionsdispatcher:permissionsdispatcher-processor:4.8.0"
    implementation "com.google.android.material:material:1.5.0"
    implementation 'commons-codec:commons-codec:1.11'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.0"
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    kapt 'com.github.bumptech.glide:compiler:4.12.0'
    implementation 'com.caverock:androidsvg-aar:1.4'
    implementation 'com.google.firebase:firebase-messaging:23.0.1'
    implementation 'com.google.firebase:firebase-analytics:20.1.0'
    implementation 'com.google.firebase:firebase-crashlytics:18.2.9'
    implementation 'com.github.ybq:Android-SpinKit:1.4.0'
    implementation "androidx.media:media:1.5.0"
    implementation 'com.google.firebase:firebase-crashlytics-ndk:18.2.9'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.2.0-alpha01'

}



